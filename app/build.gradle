plugins {
	id 'com.android.application'
	id 'org.jetbrains.kotlin.android'
	id 'com.google.devtools.ksp'
	id 'com.google.gms.google-services'
	id 'com.google.firebase.crashlytics'
}

apply from: '../ktlint.gradle'

android {
	namespace 'com.cube.sprintzerotemplate'
	compileSdk 34

	defaultConfig {
		applicationId "com.cube.sprintzerotemplate"
		minSdk 26
		targetSdk 34
		versionCode 1
		versionName "1.0"

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		vectorDrawables {
			useSupportLibrary true
		}

		def sentryDsn = System.properties.get("SENTRY_DSN")
		buildConfigField "String", "SENTRY_DSN", sentryDsn
	}

	flavorDimensions = ["firebase", "api"]
	productFlavors {
		firebaseStaging {
			dimension "firebase"
		}
		apiStaging {
			dimension "api"
			buildConfigField "String", "API_URL", "\"https://api.staging.goes.here.com\""
		}
	}

	signingConfigs {
		release {
			storeFile = file("$rootDir/keystores/keystore.keystore")
			storePassword = project.findProperty("keystore_password") ?: ""
			keyAlias = project.findProperty("key_alias") ?: ""
			keyPassword = project.findProperty("key_password") ?: ""
		}
	}

	buildTypes {
		debug {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_17
		targetCompatibility JavaVersion.VERSION_17
	}

	buildFeatures {
		viewBinding true
	}
}

dependencies {
	// Core dependencies
	implementation 'androidx.core:core-ktx:1.12.0'
	implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
	implementation 'androidx.appcompat:appcompat:1.6.1'
	implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
	implementation 'com.android.databinding:viewbinding:8.2.1'
	implementation 'androidx.recyclerview:recyclerview:1.3.2'

	// Unit test dependencies
	testImplementation 'junit:junit:4.13.2'
	androidTestImplementation 'androidx.test.ext:junit:1.1.5'
	androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

	// UI
	implementation "com.airbnb.android:lottie:6.2.0"
	implementation 'com.google.android.material:material:1.11.0'

	// Util dependencies
	implementation 'com.jakewharton.timber:timber:5.0.1'
	implementation platform('io.sentry:sentry-bom:7.2.0')
	implementation('io.sentry:sentry-android')
	implementation('io.sentry:sentry-android-timber')

	// Splash screen
	implementation 'androidx.core:core-splashscreen:1.0.1'

	// Firebase dependencies
	implementation(platform("com.google.firebase:firebase-bom:32.7.0"))
	implementation 'com.google.firebase:firebase-analytics-ktx'
	implementation 'com.google.firebase:firebase-crashlytics-ktx'
}